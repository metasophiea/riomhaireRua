#printLib.rualib

//request rule from user
    //entered rule is a binary reaction to the following situations
    // 111 110 101 100 011 010 001 000
    //thus for the following results
    // 111 110 101 100 011 010 001 000
    //  0   0   0   1   1   1   1   0
    //a user must enter "30"

    consolePort = 6
    //print machine name
    run:printRiomhreRuaAndDetails
    set:consolePort:10 //next line

    set:consolePort:114 //r
    set:consolePort:117 //u
    set:consolePort:108 //l
    set:consolePort:101 //e
    set:consolePort:32  //(space)
    set:consolePort:45  //-
    set:consolePort:62  //>
    set:consolePort:32  //(space)
    set:consolePort:0   //prompt input

//extract that value (placing it into byte 130)
    set:110:48
    ::inputLoop
        copy:6:100
        ifResultZero:inputLoop_end

        //multiply byte 130 by 100 (using 140 and 200)
            set:140:0
            set:200:10
            ::miniLoop
                ifResultZero:endMiniLoop
                add:130:140:140
                dec:200
                goto:miniLoop
            ::endMiniLoop
            copy:140:130

        //determine typed value of digit
            sub:100:110:120
        //add this value to the sum (byte 130)
            add:120:130:130

    goto:inputLoop
    ::inputLoop_end

//this is the selected rule, place this in byte 31
    ruleByte = 31
    copy:130:ruleByte

//place initial pixel
    pixelDisplayAccess = 7
    pixelDisplayAddress_y = 8
    pixelDisplayAddress_x = 9
    set:pixelDisplayAddress_x:128
    set:pixelDisplayAccess:255

//run through next line and apply rules
    inc:pixelDisplayAddress_y
    set:pixelDisplayAddress_x:0

//x byte
    set:100:0
//holder byte
    set:110:1

::mainLoop
    inc:100
    add:100:110:120
    ifResultOverflow:main_endLoop //line finished
    //select pixel we're editing
        copy:100:pixelDisplayAddress_x

    //gather the three pixels above the selected into 200 210 220
        aboveLeft = 200
        aboveCenter = 210
        aboveRight = 220

        dec:pixelDisplayAddress_y
        dec:pixelDisplayAddress_x
        copy:pixelDisplayAccess:aboveLeft
        inc:pixelDisplayAddress_x
        copy:pixelDisplayAccess:aboveCenter
        inc:pixelDisplayAddress_x
        copy:pixelDisplayAccess:aboveRight
        dec:pixelDisplayAddress_x
        inc:pixelDisplayAddress_y

    //run rules found ruleByte, placing result in workingPixel (230)
        workingPixel = 230
        temp = 30
        //reset pixel
            set:workingPixel:0

        //decision tree
            copy:aboveLeft:temp
            ifResultZero:0xx
            //1xx
                copy:aboveCenter:temp
                ifResultZero:10x
                //11x
                    copy:aboveRight:temp
                    ifResultZero:110
                    //111
                        //check rule for this situation
                        ifBitSet:ruleByte:pixelDisplayAccess:111_set
                            goto:end
                        ::111_set
                            set:workingPixel:255
                        goto:end
                    ::110
                        //check rule for this situation
                        ifBitSet:ruleByte:6:110_set
                            goto:end
                        ::110_set
                            set:workingPixel:255
                        goto:end
                    ::10x
                    copy:aboveRight:temp
                    ifResultZero:100
                    //101
                        //check rule for this situation
                        ifBitSet:ruleByte:5:101_set
                            goto:end
                        ::101_set
                            set:workingPixel:255
                        goto:end
                    ::100
                        //check rule for this situation
                        ifBitSet:ruleByte:4:100_set
                            goto:end
                        ::100_set
                            set:workingPixel:255
                        goto:end
            ::0xx
                copy:aboveCenter:temp
                ifResultZero:00x
                //01x
                    copy:aboveRight:temp
                    ifResultZero:010
                    //011
                        //check rule for this situation
                        ifBitSet:ruleByte:3:011_set
                            goto:end
                        ::011_set
                            set:workingPixel:255
                        goto:end
                    ::010
                        //check rule for this situation
                        ifBitSet:ruleByte:2:010_set
                            goto:end
                        ::010_set
                            set:workingPixel:255
                        goto:end
                    ::00x
                    copy:aboveRight:temp
                    ifResultZero:000
                    //001
                        //check rule for this situation
                        ifBitSet:ruleByte:1:001_set
                            goto:end
                        ::001_set
                            set:workingPixel:255
                        goto:end
                    ::000
                        //check rule for this situation
                        ifBitSet:ruleByte:0:000_set
                            goto:end
                        ::000_set
                            set:workingPixel:255
                        goto:end
            ::end

    //copy 230 into pixel
        copy:workingPixel:pixelDisplayAccess

    goto:mainLoop
::main_endLoop
inc:pixelDisplayAddress_y
ifResultOverflow:endProgram
goto:mainLoop
::endProgram

wait:5000